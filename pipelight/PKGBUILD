# Maintainer: Anish Bhatt anish [at] gatech [dot] edu

pkgname='pipelight'
pkgver=0.2.5
pkgrel=3
epoch=1
pkgdesc="A browser plugin which allows one to use windows only plugins inside Linux browser"
url="https://launchpad.net/pipelight"
arch=('i686' 'x86_64')
license=('GPL2' 'LGPL2.1' 'MPL')

# wine-compholio is provided for example by wine-silverlight
depends=('wine-compholio' 'ttf-ms-fonts' 'wget' 'cabextract' 'unzip' 'gnupg')
if  [[ "$CARCH" == "x86_64" ]]; then
  depends+=('lib32-libsm' 'lib32-libgl')
else
  depends+=('libsm' 'libgl')
fi

optdepends=('zenity: needed for file dialogs',
            'kdebase-kdialog: needed for file dialogs in KDE')

# Set value to 1 if you want to compile pluginloader yourself
_compilepluginloader=0

if [[ $_compilepluginloader != 0 ]]; then
  if [[ "$CARCH" == "x86_64" ]]; then
  makedepends=('mingw-w64-gcc')
  else
  makedepends=('mingw32-gcc')
  fi
fi

makedepends+=('cabextract' 'xz')

#source=("https://bitbucket.org/mmueller2012/pipelight/get/v${pkgver%.*}-${pkgver##*.}.tar.bz2"
#  "https://launchpad.net/pipelight/trunk/0.1/+download/pluginloader-prebuilt-v${pkgver%.*}-${pkgver##*.}.tar.gz")
source=("https://bitbucket.org/mmueller2012/pipelight/get/v${pkgver}.tar.bz2"
  "http://repos.fds-team.de/pluginloader/v${pkgver}/pluginloader.tar.gz")
noextract=("pluginloader.tar.gz")

md5sums=('56f6ede3bafa9f2e5cf72bf087a98315'
         '029be1ca83633ffa87ef96e6fcf5f9c5')

install=pipelight.install

_srcdir=mmueller2012-pipelight-035090b504f5

#this changes the install location for the pipelight executables
_prefix=/usr

#change this if your wine-silverlight is installed elsewhere, or if you prefer a different wine directory
_wine=/opt/wine-compholio

prepare() {
  pushd $_srcdir
  if [[ $_compilepluginloader == 0 ]]; then
    tar xvzf ../pluginloader.tar.gz
  fi
  popd
}

build() {
  pushd $_srcdir
  if [[ $_compilepluginloader == 0 ]]; then
    ./configure --prefix=$_prefix --wine-path=$_wine/bin/wine --gcc-runtime-dlls="" --win32-prebuilt
  else
    ./configure --prefix=$_prefix --wine-path=$_wine/bin/wine --gcc-runtime-dlls="" --win32-static
  fi
  make
  popd
}

package() {
  make -C $_srcdir PREFIX=$_prefix DESTDIR=$pkgdir install

# All plugin creation has now been moved to pipelight.install

# fix man page flags
  chmod 0644 ${pkgdir}/usr/share/man/man1/pipelight-plugin.1
}

# vim:set ts=2 sw=2 et:
